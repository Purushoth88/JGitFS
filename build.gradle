apply plugin: 'java'
//apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin:'application'
apply plugin:'jacoco'

sourceCompatibility = 1.7

mainClassName = "org.dstadler.jgitfs.JGitFS"

repositories {
  mavenCentral()
}

dependencies {
	compile 'org.dstadler:commons-dost:1.0.0.+'
	compile 'commons-io:commons-io:2.4'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'net.java.dev.jna:jna:3.5.2'
	compile 'org.eclipse.jgit:org.eclipse.jgit:4.0.1.201506240215-r'
	compile 'com.google.guava:guava:18.0'
	compile project(':fuse-jna')
	compile 'jline:jline:2.12.1'

	runtime 'org.slf4j:slf4j-simple:1.7.12'

	testCompile 'junit:junit:4.+'
	testCompile 'org.dstadler:commons-test:1.0.0.+'
}

// work around unnecessary timestamp in generated file which always causes dirty files in version control
// https://issues.gradle.org/browse/GRADLE-2293
task adjustEclipseSettingsFile << {
   ant.replaceregexp(match:'^#.*', replace:'', flags:'g', byline:true) {
      fileset(dir: project.projectDir, includes: '.settings/org.eclipse.jdt.core.prefs')
   }
}
task sortEclipseSettingsFile << {
   new File(project.projectDir, '.settings/org.eclipse.jdt.core.prefs').with { it.text = it.readLines().findAll { it }.sort().join('\n') }
}
eclipseJdt.finalizedBy adjustEclipseSettingsFile
eclipseJdt.finalizedBy sortEclipseSettingsFile

tasks.withType(JavaCompile){
	// avoid: :compileJavawarning: [options] bootstrap class path not set in conjunction with -source 1.7
	options.warnings = false
	options.compilerArgs = [ '-Xlint:deprecation' ]
    options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.5'
}

jacoco {
	toolVersion = '0.7.4.201502262128'
}
